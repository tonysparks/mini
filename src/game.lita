import "io";
import "raylib";
import "raygui";
import "animation";
import "common";
import "input_system";
import "console";
import "terrain";
import "camera";
import "math";
import "string_buffer";
import "mem";
import "gang";
import "building";

public const MAX_BUILDINGS = 32
public const MAX_GANGS = 4

struct Gui {
    building: Building*
    closed: bool
    buffer: StringBuffer
    font: Font
}

public struct Game {
    camera: GameCamera
    guiCamera: Camera2D
    terrain: Terrain

    gui: Gui

    buildings: [MAX_BUILDINGS]Building
    numOfBuildings: i32

    gangs: [MAX_GANGS]Gang
    numOfGangs: i32
}

const game = Game{}

func GameOnMouseEvent(event: MouseEvent*) : bool {   
    var pos = Vector2Subtract(GetMousePosition(), game.camera.camera.offset) 
    if (event.type == EventType.PRESSED && event.button == MouseButton.MOUSE_LEFT_BUTTON) {      
        for(var i = 0; i < game.numOfBuildings; i+=1) {
            var b = &game.buildings[i]
            if(b.bounds.containsV(pos)) {
                game.gui.building = b
                game.gui.closed = false
                return true
            }
        }
    }

    return false
}

func GameOnKeyEvent(event: KeyEvent*) : bool {
    if (event.type == EventType.RELEASED && event.keyCode == KeyboardKey.KEY_SPACE) {        
        return true;
    }
    return false
}

public func GameInit() {
    game.camera.init()

    game.guiCamera = Camera2D {
        .offset: Vec2{0,0}, 
        .target: Vec2{0,0},
        .rotation: 0,
        .zoom: 1f,
    }

    game.gui.buffer.init(32)
    game.gui.building = null
    game.gui.font = LoadFont("../assets/Consola.ttf")

    var filename = "../assets/mini_city.json"
        //"../assets/level2.txt"
    var data : char* = null
    var status = readFile(filename, &data);
    if(status != FileStatus.Ok) {
        log(LogLevel.FATAL, "*** Unable to open %s\n", filename)
    }

    game.terrain.init(data)


    // TEMP
    game.gangs[0].init("Three One's");
    game.gangs[1].init("Lord's");
    game.gangs[2].init("Disciple's");
    game.numOfGangs = 3

    var member = new<GangMember>()
    member.init()

    var gang = &game.gangs[0]
    gang.addMember(member)

    game.buildings[0] = Building {
        .name: "4647 S. 31st St.",
        .bounds: Rectangle{256, 192, 128, 160},
        .loyalty: 32,
        .activities: (Activity.DRUGS|Activity.ARMS),
        .affiliated: &game.gangs[0],
    }
    game.numOfBuildings = 1
    // END TEMP

    InputSysRegisterMouseHandler(&GameOnMouseEvent)
    InputSysRegisterKeyboardHandler(&GameOnKeyEvent)
}

public func GameDestroy() {    
    game.terrain.destroy()
}

public func GameUpdate(timeStep: TimeStep*) {
    game.camera.update(timeStep, &game)

    for(var i = 0; i < game.numOfGangs; i += 1) {
        var g = &game.gangs[i]
        g.update(timeStep)
    }
}

public func GameDraw() {
    BeginMode2D(game.camera.camera)
    {
        game.terrain.draw(&game.camera)
        for(var i = 0; i < game.numOfGangs; i += 1) {
            var g = &game.gangs[i]
            g.draw()
        }
    }
    EndMode2D()

    BeginMode2D(game.guiCamera)
    {
        DrawGui()
    }
    EndMode2D()
}

func DrawGui() {    
    var g = &game.gui
    if(g.closed || !g.building) {
        return;
    }

    GuiFont(g.font)
    GuiSetStyle(GuiControl.LABEL, GuiControlProperty.TEXT_COLOR_NORMAL, 0x000000ff)
    GuiSetStyle(GuiControl.DEFAULT, GuiDefaultProperty.TEXT_SIZE, 12)

    g.closed = GuiWindowBox(Rectangle{ 5, 5, 240, 400 }, g.building.name);
    
    //GuiCheckBox(Rectangle{ 25, 108, 15, 15 }, "Force Square", true);

    
    g.buffer.append("Loyalty to you:")
    GuiLabel(Rectangle{ 10, 30, 100, 25 }, g.buffer.cStr()); 
    g.buffer.clear()

    GuiSetStyle(GuiControl.LABEL, GuiControlProperty.TEXT_COLOR_NORMAL, 0xff0000ff)
    g.buffer.append("%d / 100", g.building.loyalty)
    GuiLabel(Rectangle{ 130, 30, 100, 25 }, g.buffer.cStr());
    g.buffer.clear()
    
    GuiSetStyle(GuiControl.LABEL, GuiControlProperty.TEXT_COLOR_NORMAL, 0x000000ff)
    g.buffer.append("Affiliated with:")
    GuiLabel(Rectangle{ 10, 50, 100, 25 }, g.buffer.cStr()); 
    g.buffer.clear()

    
    GuiSetStyle(GuiControl.LABEL, GuiControlProperty.TEXT_COLOR_NORMAL, 0xff0000ff)    
    g.buffer.append("%s", (g.building.affiliated != null) ? g.building.affiliated.name as (char*) : "--")
    GuiLabel(Rectangle{ 130, 50, 100, 25 }, g.buffer.cStr()); 
    g.buffer.clear()


    var a = g.building.activities
    var x = 15
    var y = 90
    
    GuiSetStyle(GuiControl.LABEL, GuiControlProperty.TEXT_COLOR_NORMAL, 0x000000ff)

    GuiLock()
    GuiCheckBox(Rectangle{ x, y, 15, 15 }, "Drug House", a & Activity.DRUGS);
    y += 25
    GuiCheckBox(Rectangle{ x, y, 15, 15 }, "Prostitution Ring", a & Activity.PROSTITUTION);
    y += 25
    GuiCheckBox(Rectangle{ x, y, 15, 15 }, "Arms", a & Activity.ARMS);
    y += 25
    GuiCheckBox(Rectangle{ x, y, 15, 15 }, "Money Laundering", a & Activity.LAUNDER);
    y += 25
    GuiCheckBox(Rectangle{ x, y, 15, 15 }, "Safe Haven", a & Activity.HAVEN);
    y += 25
    GuiUnlock()

    //GuiLabel(Rectangle{ 0, 20, 100, 25 }, "test"); 
    
}

func DrawBuildingInfoGui() {

}